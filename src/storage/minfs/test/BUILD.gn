# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//build/testing/bootfs_test.gni")
import("//src/lib/isolated_devmgr/v2_component/isolated_devmgr.gni")
import("//src/sys/build/fuchsia_test_package.gni")
import("//src/sys/build/fuchsia_unittest_package.gni")

test("minfs_unit") {
  sources = [
    "unit/bcache_test.cc",
    "unit/buffer_view_test.cc",
    "unit/command_handler_test.cc",
    "unit/disk_struct_test.cc",
    "unit/format_test.cc",
    "unit/fsck_test.cc",
    "unit/inspector_test.cc",
    "unit/journal_integration_fixture.cc",
    "unit/journal_test.cc",
    "unit/lazy_buffer_test.cc",
    "unit/lazy_reader_test.cc",
    "unit/loader_test.cc",
    "unit/minfs_inspector_test.cc",
    "unit/mkfs_test.cc",
    "unit/mount_test.cc",
    "unit/parser_test.cc",
    "unit/resizeable_array_buffer_test.cc",
    "unit/resizeable_vmo_buffer_test.cc",
    "unit/superblock_test.cc",
    "unit/transaction_limits_test.cc",
    "unit/transaction_test.cc",
    "unit/truncate_test.cc",
    "unit/unlink_test.cc",
    "unit/vnode_mapper_test.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_c",
    "//src/lib/cobalt/cpp/testing:mock_cobalt_logger",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/fvm",
    "//src/storage/minfs",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/safemath",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/block-client",
    "//zircon/system/ulib/block-client:fake-device",
    "//zircon/system/ulib/cobalt-client:in-memory-logger",
    "//zircon/system/ulib/disk_inspector",
    "//zircon/system/ulib/fs-management",
    "//zircon/third_party/ulib/cksum",
  ]
}

test("minfs-integration") {
  sources = [
    "integration/backup_superblock.cc",
    "integration/mount_test.cc",
    "integration/power_test.cc",
    "integration/runtime_state_test.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_llcpp",
    "//src/devices/tests/sysdev",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/fs_test",
    "//src/storage/fvm",
    "//src/storage/minfs",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fs-management",
  ]
}

test("minfs_dirty_cache_integration") {
  sources = [ "integration/dirty_cache_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.fs",
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_llcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/isolated_devmgr/v2_component:client",
    "//src/storage/fs_test",
    "//src/storage/minfs",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fs-management",
  ]
}

test("minfs-power") {
  sources = [ "integration/power_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/fs_test",
    "//src/storage/memfs",
    "//src/storage/minfs",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

test("minfs_micro_benchmark") {
  sources = [
    "micro-benchmark/minfs-costs.cc",
    "micro-benchmark/minfs-tests.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_c",
    "//sdk/lib/fdio",
    "//src/lib/digest",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/isolated_devmgr/v2_component:client",
    "//src/storage/fs_test",
    "//src/storage/fvm",
    "//src/storage/gpt",
    "//src/storage/memfs",
    "//src/storage/memfs:memfs-cpp",
    "//src/storage/minfs",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fs",
    "//zircon/system/ulib/fs-management",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/perftest",
    "//zircon/system/ulib/ramdevice-client",
    "//zircon/system/ulib/storage-metrics",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-engine",
  ]
}

fuzzer("minfs-fuzzer") {
  sources = [ "unit/fuzz.cc" ]
  deps = [
    "//src/storage/minfs",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/block-client:fake-device",
  ]
}

fuzzers_package("minfs-fuzzers") {
  cpp_fuzzers = [ ":minfs-fuzzer" ]
}

isolated_devmgr_unittest_v2_component("minfs-integration-test") {
  executable_path = "test/minfs-integration"
  deps = [ ":minfs-integration" ]
}

isolated_devmgr_unittest_v2_component("minfs-dirty-cache-integration-test") {
  executable_path = "test/minfs_dirty_cache_integration"
  deps = [ ":minfs_dirty_cache_integration" ]
}

isolated_devmgr_unittest_v2_component("minfs-power-test") {
  executable_path = "test/minfs-power"
  deps = [ ":minfs-power" ]
}

fuchsia_unittest_component("minfs-unit-test") {
  deps = [ ":minfs_unit" ]
  manifest = "meta/minfs_unit_test.cml"
}

isolated_devmgr_unittest_v2_component("minfs-micro-benchmark-test") {
  executable_path = "test/minfs_micro_benchmark"
  deps = [ ":minfs_micro_benchmark" ]
}

fuchsia_test_package("minfs-tests") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }

  test_components = [
    ":minfs-integration-test",
    ":minfs-dirty-cache-integration-test",
    ":minfs-power-test",
    ":minfs-unit-test",
    ":minfs-micro-benchmark-test",
  ]
}
